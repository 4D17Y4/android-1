buildscript {
    repositories {
        mavenCentral()
        maven {
            url "http://download.crashlytics.com/maven"
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:0.12.+"
        classpath "com.crashlytics.tools.gradle:crashlytics-gradle:1.+"
        classpath "com.jakewharton.sdkmanager:gradle-plugin:0.12.+"
    }
}

apply plugin: "android-sdk-manager"
apply plugin: "android"
apply plugin: "crashlytics"

android {
    compileSdkVersion 19
    buildToolsVersion "20"

    defaultConfig {
        def versionMajor = 0
        def versionMinor = 4
        def versionPatch = 2

        versionName buildVersionName(versionMajor, versionMinor, versionPatch)
        versionCode buildVersionCode(versionMajor, versionMinor, versionPatch)

        minSdkVersion 14
        targetSdkVersion 19

        def apiFile = file("api.properties")
        def apiProperties = new Properties()

        if (apiFile.exists()) {
            apiProperties.load(apiFile.newInputStream())

            buildConfigField "String", "API_URL_AMAHI", formatStringField(apiProperties["url.amahi"])
            buildConfigField "String", "API_URL_PROXY", formatStringField(apiProperties["url.proxy"])
            buildConfigField "String", "API_CLIENT_ID", formatStringField(apiProperties["client.id"])
            buildConfigField "String", "API_CLIENT_SECRET", formatStringField(apiProperties["client.secret"])
        }
    }
}

def buildVersionName(versionMajor, versionMinor, versionPatch) {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

def buildVersionCode(versionMajor, versionMinor, versionPatch) {
    return versionMajor * 10 ** 2 + versionMinor * 10 ** 1 + versionPatch * 10 ** 0
}

def formatStringField(field) {
    return "\"${field}\""
}

dependencies {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url "http://download.crashlytics.com/maven"
        }
    }

    compile "com.android.support:support-v4:20.+"
    compile "com.android.support:support-v13:20.+"
    compile "com.crashlytics.android:crashlytics:1.+"
    compile "com.github.dmytrodanylyk.android-process-button:library:1.0.+"
    compile "com.jakewharton.byteunits:byteunits:0.9.+"
    compile "com.jakewharton.timber:timber:2.4.+"
    compile "com.squareup.dagger:dagger:1.2.+"
    compile "com.squareup.okhttp:okhttp:2.0.+"
    compile "com.squareup.okhttp:okhttp-urlconnection:2.0.+"
    compile "com.squareup:otto:1.3.+"
    compile "com.squareup.picasso:picasso:2.3.+"
    compile "com.squareup.retrofit:retrofit:1.6.+"
    compile "org.videolan:libvlc:0.9.+"

    provided "com.squareup.dagger:dagger-compiler:1.2.+"
}

task generateWrapper(type: Wrapper) {
    gradleVersion = "1.12"
}
