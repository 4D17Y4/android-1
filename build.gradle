buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:0.10.+"
        classpath "com.jakewharton.sdkmanager:gradle-plugin:0.10.+"
    }
}

apply plugin: "android-sdk-manager"
apply plugin: "android"

android {
    compileSdkVersion 19
    buildToolsVersion "19.1"

    defaultConfig {
        versionName "1.0.0"
        versionCode 100

        minSdkVersion 14
        targetSdkVersion 19

        def apiFile = file("api.properties")
        def apiProperties = new Properties()

        if (apiFile.exists()) {
            apiProperties.load(apiFile.newInputStream())

            buildConfigField "String", "API_URL_AMAHI", formatStringField(apiProperties["url.amahi"])
            buildConfigField "String", "API_URL_PROXY", formatStringField(apiProperties["url.proxy"])
            buildConfigField "String", "API_CLIENT_ID", formatStringField(apiProperties["client.id"])
            buildConfigField "String", "API_CLIENT_SECRET", formatStringField(apiProperties["client.secret"])
            buildConfigField "String", "API_CLIENT_TOKEN", formatStringField(apiProperties["client.token"])
        }
    }
}

def formatStringField(field) {
    return String.format("\"%s\"", field)
}

dependencies {
    repositories {
        mavenCentral()
        mavenLocal()
    }

    compile "com.android.support:support-v4:19.1.+"
    compile "com.jakewharton.timber:timber:2.3.+"
    compile "com.squareup.dagger:dagger:1.2.+"
    compile "com.squareup.okhttp:okhttp:1.5.+"
    compile "com.squareup:otto:1.3.+"
    compile "com.squareup.picasso:picasso:2.2.+"
    compile "com.squareup.retrofit:retrofit:1.5.+"
    compile "org.videolan:libvlc:0.9.+"

    provided "com.squareup.dagger:dagger-compiler:1.2.+"
}
